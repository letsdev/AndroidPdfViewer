apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'net.researchgate.release'

def publishedGroupIdVar = 'com.github.barteksc'
def libraryNameVar = 'AndroidPdfViewer'
def artifactVar = 'android-pdf-viewer'

ext {
    bintrayRepo = 'maven'
    bintrayName = 'android-pdf-viewer'

    publishedGroupId = publishedGroupIdVar
    libraryName = libraryNameVar
    artifact = artifactVar

    libraryDescription = 'Android view for displaying PDFs rendered with PdfiumAndroid'

    siteUrl = 'https://github.com/barteksc/AndroidPdfViewer'
    gitUrl = 'https://github.com/barteksc/AndroidPdfViewer.git'

    libraryVersion = '2.5.1'

    developerId = 'barteksc'
    developerName = 'Bartosz Schiller'
    developerEmail = 'barteksch@boo.pl'

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ["Apache-2.0"]
}

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.3'

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    lintOptions {
        abortOnError false
    }

    defaultConfig {
        minSdkVersion 11
        targetSdkVersion 25
        versionCode System.getenv("BUILD_NUMBER") as Integer ?: 0
        versionName "2.5.1"
    }

    def getMavenSettingsCredentials = {
        String userHome = System.getProperty("user.home");
        File mavenSettings = new File(userHome, ".m2/settings.xml")
        def xmlSlurper = new XmlSlurper()
        def output = xmlSlurper.parse(mavenSettings)
        return output."servers"."server"
    }

    def getCredentials = {
        def entries = getMavenSettingsCredentials()
        for (entry in entries) {
            return [username: entry.username.text(), password: entry.password.text()]
        }
    }

    def localRepositoryPath = System.getProperty('user.home') + '/.m2/repository/com/github/barteksc/android-pdf-viewer'

    uploadArchives {
        if (debug == 'true') {
            repositories {
                flatDir {
                    dir localRepositoryPath
                }
            }
        } else {
            def creds = getCredentials()
            repositories {
                mavenDeployer {
                    repository(url: "http://dev2/nexus/repository/ld-third-party-releases/") {
                        authentication(userName: creds["username"], password: creds["password"])
                    }

                    pom.version = android.defaultConfig.versionName
                    pom.artifactId = artifactVar
                    pom.groupId = publishedGroupIdVar
                }
            }
        }
    }
}

dependencies {
    compile 'com.github.barteksc:pdfium-android:1.5.0'
}